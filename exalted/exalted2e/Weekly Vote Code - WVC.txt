@@ DEPENDENCIES - CORE

@select/inline isdbref(u(wvc))=0,{@tel create(Weekly Vote Code <WVC>)=config(master_room)}
&WVC u(coi)=locate(config(master_room),Weekly Vote Code <WVC>,TXxi)
@parent u(wvc)=u(coi)
@set u(wvc)=WIZARD !NO_COMMAND

&CMD`+VOTE u(wvc)=$^\+vote(?\:/(\S+))?(?\: +(.+))?$:@attach u(ccs)/INC`PARTIAL=%1,setunion(v(VAR`PLAYFLAGS),if(isadmin(%#),v(VAR`ADMINFLAGS)),|,|),|,VOTE,switch,switch;@attach u(wvc)/INC`[strfirstof(%q<switch>,MAIN)]=%2
@set u(wvc)/CMD`+VOTE=regexp

&VAR`PLAYFLAGS u(wvc)=CHECK
&VAR`ADMINFLAGS u(wvc)=PROCESS

&INC`MAIN u(wvc)=@attach u(ccs)/INC`CHECKPC=%0,1,VOTE;@check approved(%#)=@pemit %#=msghead(VOTE) You are not approved!;@check approved(%q<t1>)=@pemit %#=msghead(VOTE) [ansi(h,%q<t1name>)] is not an approved character!;@stop strmatch(%#,%q<t1>)=@pemit %#=msghead(VOTE) You may not vote for yourself!;@check gt(u(setr,diff,sub(secs(),strfirstof(get(%q<t1>/D`VOTE`LAST),0))),mul(3600,get(u(wvc)/VAR`HOURS)))=@pemit %#=msghead(VOTE) [ansi(h,name(%q<t1>))] may not receive a vote for another [ansi(h,etime(sub(mul(3600,get(u(wvc)/VAR`HOURS)),%q<diff>)))];&D`VOTE`COUNT %q<t1>=add(get(%q<t1>/D`VOTE`COUNT),1);&D`VOTE`VOTERS %q<t1>=setunion(get(%q<t1>/D`VOTE`VOTERS),%n,|,|);&D`VOTE`LAST %q<t1>=secs();@pemit %#=msghead(VOTE) You have added a vote for [ansi(h,%q<t1name>)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,%n) just added a vote for [ansi(h,%q<t1name>)]

&INC`CHECK u(wvc)=@select/inline and(isadmin(%#),gt(strlen(%2),0))=1,{@attach u(ccs)/INC`CHECKPC=%0,1,VOTE},0,{@attach u(ccs)/INC`CHECKPC=%#,1,VOTE};@pemit %#=header(%q<t1name>'s Votes)%RCurrent Votes: [get(%q<t1>/D`VOTE`COUNT)]%RLast Vote: [convsecs(get(%q<t1>/D`VOTE`LAST))]%RVoters: [itemize(get(%q<t1>/D`VOTE`VOTERS),|)]%R[if(strmatch(%#,%q<t1>),You,capnames(subj(%q<t1>)))] can get another vote: [if(gt(u(setr,diff,sub(secs(),strfirstof(get(%q<target2>/D`VOTE`LAST),0))),mul(3600,get(u(wvc)/VAR`HOURS))),Now!,in [ansi(h,etime(sub(mul(3600,get(u(wvc)/VAR`HOURS)),%q<diff>)))])]%R[header()]

&INC`PROCESS u(wvc)=@attach u(ccs)/INC`CHECKPC=%0,1,VOTE;@check [u(setr,v,get(%q<t1>/D`VOTE`COUNT))][u(setq,c,switch(1,lte(u(setr,x,u(u(ex-ept)/FUN`COUNT,%q<t1>,G)),649),switch(%qv,1,10,2,11,3,12,12),gte(%qx,850),1,gte(%qx,650),switch(%qv,1,5,2,6,3,6,4,7,7)))]=@pemit %#=That player has no votes.;@force/inline %#=+xp/award %qv votes for [time()] from [u(setr,voters,itemize(get(%q<t1>/D`VOTE`VOTERS),|,and,\,))]=%q<t1>=%qc;&D`VOTE`COUNT %q<t1>=0;@wipe %q<t1>/D`VOTE`LAST;@wipe %q<t1>/D`VOTE`VOTERS;@pemit %q<t1>=msghead(VOTE) You have been awarded [ansi(h,%qc)] XP for this week's votes.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,%n) has awarded [ansi(h,%q<t1>)] %qc xp for this week's votes from %q<voters>

&VAR`HOURS u(wvc)=8

&VAR`CHANNEL u(wvc)=Staff Reports